! Input/Output Tool Kit (IOTK)
! Copyright (C) 2004-2006 Giovanni Bussi
!
! This library is free software; you can redistribute it and/or
! modify it under the terms of the GNU Lesser General Public
! License as published by the Free Software Foundation; either
! version 2.1 of the License, or (at your option) any later version.
!
! This library is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
! Lesser General Public License for more details.
!
! You should have received a copy of the GNU Lesser General Public
! License along with this library; if not, write to the Free Software
! Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

!------------------------------------------------------------------------------!
! Inclusion of configuration file
#include "iotk_config.h"
!------------------------------------------------------------------------------!

>##############################################################################<
># inclusion of the spp definitions
>include iotk_include.sh
>##############################################################################<
># inclusion of the auxiliary macros
#include "iotk_auxmacros.h"
>##############################################################################

> REVISION='$Revision$'
> REVISION="${REVISION//${dol}/}"

>PROCEDURE=iotk_copy_tag
subroutine iotk_copy_tag_x(source,dest,dummy,maxsize,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_attr_interf
  use iotk_scan_interf
  use iotk_write_interf
  use iotk_str_interf
  use iotk_unit_interf
  use iotk_misc_interf
  implicit none
  integer,           intent(in)  :: source
  integer,           intent(in)  :: dest
  type(iotk_dummytype),optional  :: dummy
  integer, optional, intent(in)  :: maxsize
  integer, optional, intent(out) :: ierr
  logical :: source_binary,dest_binary,source_stream,dest_stream
  integer :: ierrl,control,maxsizel
  character(iotk_taglenx) :: tag
  character(iotk_namlenx) :: name
  character(iotk_attlenx) :: attr
  character(iotk_vallenx) :: type
  type(iotk_unit), pointer :: this
  ierrl = 0         
  maxsizel = -1     
  if(present(maxsize)) maxsizel = maxsize 
  call iotk_inquire(source,source_binary,source_stream,ierrl)
  if(ierrl/=0) then
    $(ERROR ierrl)
    goto 1
  end if
  call iotk_inquire(dest,dest_binary,dest_stream,ierrl)
  if(ierrl/=0) then
    $(ERROR ierrl)            
    goto 1
  end if
  call iotk_unit_get(source,pointer=this)
  if(.not.associated(this)) then
    $(ERROR ierrl unit)  
    goto 1
  end if
  do
    call iotk_scan_tag(source,+1,control,tag,source_binary,source_stream,ierrl)
    if(ierrl/=0) then
      $(ERROR ierrl)
      goto 1
    end if
    if(control/=4) then ! SKIP FOR COMMENTS
      call iotk_tag_parse(tag,name,attr,ierrl)
      if(ierrl/=0) then
        $(ERROR ierrl)
        goto 1
      end if
    end if
    if(iotk_strcomp(name,this%root)) then
      call iotk_scan_tag(source,-1,control,tag,source_binary,source_stream,ierrl)
      if(ierrl/=0) then
        $(ERROR ierrl)
        goto 1
      end if
      return
    end if
    select case(control)
    case(1)
      call iotk_scan_attr(attr,"type",type,ierr=ierrl,eos=.true.,default=" ")
      if(ierrl/=0) then
        $(ERROR ierrl)
        goto 1
      end if
      if((iotk_strcomp(type,"real") .or. iotk_strcomp(type,"integer") .or. iotk_strcomp(type,"logical") &
                                    .or. iotk_strcomp(type,"character") .or. iotk_strcomp(type,"complex")) .and. control==1) then
        call iotk_copy_dat(source,dest,source_binary,dest_binary,name,attr,maxsize=maxsizel,ierr=ierrl)
        if(ierrl/=0) then
          $(ERROR ierrl)
          goto 1
        end if
        call iotk_scan_tag(source,+1,control,tag,source_binary,source_stream,ierrl)
      else
        call iotk_write_begin(dest,name,attr,ierr=ierrl)
      end if
    case(2)
      call iotk_write_end(dest,name,ierr=ierrl)
    case(3)
      call iotk_write_empty(dest,name,attr,ierr=ierrl)
    case(4)
      call iotk_write_comment(dest,tag,ierr=ierrl)
    case(5)
      call iotk_write_pi(dest,name,attr,ierr=ierrl)
    end select
    if(ierrl/=0) then
      $(ERROR ierrl)
      goto 1
    end if
  end do
1 continue
  if(present(ierr)) then
    ierr = ierrl
  else
    if(ierrl/=0) call iotk_error_handler(ierrl)
  end if
end subroutine iotk_copy_tag_x

>PROCEDURE=iotk_parse_dat
subroutine iotk_parse_dat_x(attr,type,ikind,isize,ilen,fmt,columns,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_attr_interf
  use iotk_scan_interf
  use iotk_str_interf
  use iotk_misc_interf
  implicit none
  character(len=*), intent(in)  :: attr
#ifdef __IOTK_WORKAROUND6
  character(len=*)              :: type
#else
  character(len=*), intent(out) :: type
#endif
  integer,          intent(out) :: ikind
  integer,          intent(out) :: isize
  integer,          intent(out) :: ilen
#ifdef __IOTK_WORKAROUND6
  character(len=*)              :: fmt
#else
  character(len=*), intent(out) :: fmt
#endif
  integer,          intent(out) :: columns
  integer,          intent(out) :: ierr
  character(iotk_vallenx) :: typename
  ierr = 0
  call iotk_scan_attr(attr,"type",typename,ierr=ierr,eos=.true.,default=iotk_eos)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  type = iotk_toupper(typename)
  call iotk_scan_attr(attr,"kind",ikind,ierr=ierr,default=-1)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  call iotk_scan_attr(attr,"size",isize,ierr=ierr,default=-1)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  call iotk_scan_attr(attr,"len", ilen, ierr=ierr,default=-1)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  call iotk_scan_attr(attr,"fmt", fmt, ierr=ierr,eos=.true.,default="!"//iotk_eos)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  call iotk_scan_attr(attr,"columns",columns,ierr=ierr,default=1)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
end subroutine iotk_parse_dat_x

>PROCEDURE=iotk_set
subroutine iotk_set_x(dummy,unitmin,unitmax,getline_buffer,error_warn_overflow, &
                      linlen,indent,maxindent,error_unit,output_unit,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_misc_interf
  implicit none
  type(iotk_dummytype),optional  :: dummy
  integer, optional, intent(in)  :: unitmin
  integer, optional, intent(in)  :: unitmax
  integer, optional, intent(in)  :: getline_buffer
  logical, optional, intent(in)  :: error_warn_overflow
  integer, optional, intent(in)  :: linlen
  integer, optional, intent(in)  :: indent
  integer, optional, intent(in)  :: maxindent
  integer, optional, intent(in)  :: error_unit
  integer, optional, intent(in)  :: output_unit
  integer, optional, intent(out) :: ierr
  integer :: ierrl
  ierrl = 0
  if(present(error_warn_overflow)) then
    iotk_error_warn_overflow = error_warn_overflow
  end if
  if(present(unitmin)) then
    if(unitmin<0) then
      $(ERROR ierrl 'Wrong value for unitmin' unitmin)
      goto 1 
    end if
    iotk_unitmin = unitmin 
  end if
  if(present(unitmax)) then
    if(unitmax<0) then
      $(ERROR ierrl 'Wrong value for unitmax' unitmax)
      goto 1
    end if
    iotk_unitmax = unitmax
  end if
  if(iotk_unitmin>iotk_unitmax) then
    $(ERROR ierrl 'Inconsistency: unitmin should be less then unitmax' iotk_unitmin iotk_unitmax)
    goto 1
  end if
  if(present(getline_buffer)) then
    if(getline_buffer<1) then
      $(ERROR ierrl 'Wrong value for getline_buffer' getline_buffer)
      goto 1
    end if
    iotk_getline_buffer = getline_buffer
  end if
  if(present(linlen)) then
    if(linlen<1) then
      $(ERROR ierrl 'Wrong value for linlen' linlen)
      goto 1
    end if
    iotk_linlen = linlen
  end if
  if(present(indent)) then
    if(indent<0) then
      $(ERROR ierrl 'Wrong value for indent' indent)
      goto 1
    end if
    iotk_indent = indent
  end if
  if(present(maxindent)) then
    if(maxindent<0 .or. maxindent>iotk_linlenx) then
      $(ERROR ierrl 'Wrong value for maxindent, should be between 0 and iotk_linlenx' maxindent iotk_linlenx)
      goto 1
    end if
    iotk_maxindent = maxindent
  end if
  if(present(error_unit)) then
    if(error_unit<0) then
      $(ERROR ierrl 'Wrong value for error_unit' error_unit)
      goto 1
    end if
    iotk_error_unit = error_unit
  end if
  if(present(output_unit)) then
    if(output_unit<0) then
      $(ERROR ierrl 'Wrong value for output_unit' output_unit)
      goto 1
    end if
    iotk_output_unit = output_unit
  end if
1 continue 
  if(present(ierr)) then
    ierr = ierrl
  else
    if(ierrl/=0) call iotk_error_handler(ierrl)
  end if
end subroutine iotk_set_x

>PROCEDURE=iotk_get
subroutine iotk_get_x(dummy,unitmin,unitmax,getline_buffer,error_warn_overflow, &
                              linlen,indent,maxindent,error_unit,output_unit)
  use iotk_base
  use iotk_misc_interf
  implicit none
  type(iotk_dummytype),optional  :: dummy
  integer, optional, intent(out) :: unitmin
  integer, optional, intent(out) :: unitmax
  integer, optional, intent(out) :: getline_buffer
  logical, optional, intent(out) :: error_warn_overflow
  integer, optional, intent(out) :: linlen
  integer, optional, intent(out) :: indent
  integer, optional, intent(out) :: maxindent
  integer, optional, intent(out) :: error_unit
  integer, optional, intent(out) :: output_unit
  if(present(unitmin)) unitmin = iotk_unitmin
  if(present(unitmax)) unitmax = iotk_unitmax
  if(present(getline_buffer)) getline_buffer = iotk_getline_buffer
  if(present(error_warn_overflow)) error_warn_overflow = iotk_error_warn_overflow
  if(present(linlen)) linlen = iotk_linlen
  if(present(indent)) indent = iotk_indent
  if(present(maxindent)) maxindent = iotk_maxindent
  if(present(error_unit)) error_unit = iotk_error_unit
  if(present(output_unit)) output_unit = iotk_output_unit
end subroutine iotk_get_x

>PROCEDURE=iotk_print_kinds
subroutine iotk_print_kinds_x
  use iotk_base
  use iotk_misc_interf
  use iotk_xtox_interf
  implicit none
  character(100) :: string
  write(*,"(a,i5)") "Maximum rank            : ", iotk_maxrank
  write(*,"(a,i5)") "Maximum rank hard limit : ", iotk_maxrank
> for kind in $kinds   ; do
#ifdef __IOTK_LOGICAL${kind}
  string = "logical(kind="//trim(iotk_itoa(iotk_LOGICAL${kind}))//")"
  write(*,"(a)") trim(string)
#endif
> done
> for kind in $kinds   ; do
#ifdef __IOTK_INTEGER${kind}
  string = "integer(kind="//trim(iotk_itoa(iotk_INTEGER${kind}))//")"
  write(*,"(a)") trim(string)
#endif
> done      
> for kind in $kinds   ; do
#ifdef __IOTK_REAL${kind}
  string = "real(kind="//trim(iotk_itoa(iotk_REAL${kind}))//")"
  write(*,"(a)") trim(string)
#endif
> done                      
  string = "character(kind="//trim(iotk_itoa(iotk_CHARACTER1))//")"
  write(*,"(a)") trim(string)
end subroutine iotk_print_kinds_x


>PROCEDURE=iotk_copy_dat_aux
subroutine iotk_copy_dat_aux_x(source,dest,source_binary,dest_binary,name,type,ikind,isize, &
                               ilen,fmt,columns,attr,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_dat_interf
  use iotk_scan_interf
  use iotk_str_interf
  use iotk_misc_interf
  implicit none
  integer,      intent(in)  :: source
  integer,      intent(in)  :: dest
  logical,      intent(in)  :: source_binary
  logical,      intent(in)  :: dest_binary
  character(*), intent(in)  :: name
  character(*), intent(in)  :: type
  integer,      intent(in)  :: ikind
  integer,      intent(in)  :: isize
  integer,      intent(in)  :: ilen
  character(*), intent(in)  :: fmt
  integer,      intent(in)  :: columns
  character(*), intent(in)  :: attr
  integer,      intent(out) :: ierr
  
  integer :: tmpkind
> for type in $types ; do
> for kind in $kinds ; do
> if [ $type != CHARACTER -o $kind -eq 1 ] ; then
#ifdef __IOTK_$type$kind
> if [ $type = CHARACTER ] ; then
  $type (kind=iotk_$type$kind,len=ilen), allocatable :: dat_$type$kind (:)
> else
  $type (kind=iotk_$type$kind), allocatable :: dat_$type$kind (:)
> fi
#endif
> fi
> done
> done

! Here is the rule:
! IF SOURCE IS BINARY: use the kind of source
! IF SOURCE IS TEXTUAL: use the default kind if available
!                       otherwise use the first kind found
! anyway, kind is computed runtime and in a future implementation
! it might be also asked to the user
!
  ierr=0
  select case(type(1:iotk_strlen(type)))
> for type in $types ; do
  case("$type")
> if [ $type = CHARACTER ] ; then
    tmpkind=iotk_${type}_defkind
> else
    if(source_binary) then
      tmpkind=ikind
    else
      tmpkind=0
> for kind in $kinds ; do
#ifdef __IOTK_$type$kind
      if(tmpkind==0) tmpkind=iotk_$type$kind
      if(iotk_$type$kind == iotk_${type}_defkind) then
        tmpkind=iotk_${type}_defkind
      end if
#endif
> done
    end if
> fi
    select case(tmpkind)
> for kind in $kinds ; do
> if [ $type != CHARACTER -o $kind -eq 1 ] ; then
#ifdef __IOTK_$type$kind
    case(iotk_$type$kind)
      allocate(dat_$type$kind(isize))
      call iotk_scan_dat_aux(source,dat_$type$kind,ikind,ilen,fmt,ierr)
      if(ierr==0) call iotk_write_dat(dest,name,dat_$type$kind,attr=attr,ierr=ierr,fmt=fmt,columns=columns)
      deallocate(dat_$type$kind)
#endif
> fi
> done
    case default
      $(ERROR ierr 'internal error' )
    end select
> done
  case default
    $(ERROR ierr 'internal error')
  end select
  
end subroutine iotk_copy_dat_aux_x


>PROCEDURE=iotk_copy_dat
subroutine iotk_copy_dat_x(source,dest,source_binary,dest_binary,name,attr,maxsize,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_attr_interf
  use iotk_write_interf
  use iotk_str_interf
  use iotk_misc_interf
  implicit none
  integer,      intent(in)  :: source
  integer,      intent(in)  :: dest
  logical,      intent(in)  :: source_binary
  logical,      intent(in)  :: dest_binary
  character(*), intent(in)  :: name
  character(*), intent(in)  :: attr
  integer,      intent(in)  :: maxsize
  integer,      intent(out) :: ierr
  character(9) :: type
  integer :: ikind,isize,ilen,columns
  character(iotk_vallenx) :: fmt
  character(iotk_attlenx) :: attr1
  ierr = 0
  call iotk_parse_dat(attr,type,ikind,isize,ilen,fmt,columns,ierr)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  if(iotk_strcomp(type,iotk_eos)) then
    $(ERROR ierr)
    return
  end if
  if(isize==-1) then
    $(ERROR ierr)
    return
  end if
  if(ilen==-1 .and. iotk_strcomp(type,"CHARACTER")) then
    $(ERROR ierr)
    return
  end if
  if(isize<=maxsize .or. maxsize==-1 .or. dest_binary) then
    call iotk_copy_dat_aux(source,dest,source_binary,dest_binary,name,type,ikind,isize, &
                           ilen,fmt,columns,attr,ierr)
    if(ierr/=0) then
      $(ERROR ierr)
      return
    end if  
  else    
    call iotk_strcpy(attr1,attr,ierr=ierr)
    if(ierr/=0) then
      $(ERROR ierr)
      return
    end if
    call iotk_write_attr (attr1,"trunc",.true.,ierr=ierr)
    if(ierr/=0) then
      $(ERROR ierr)
      return
    end if
    call iotk_write_empty(dest,name,attr=attr1,ierr=ierr)
    if(ierr/=0) then
      $(ERROR ierr)
      return
    end if
  end if
end subroutine iotk_copy_dat_x

>PROCEDURE=iotk_check_iotk_attr
subroutine iotk_check_iotk_attr_x(unit,attr,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_attr_interf
  use iotk_scan_interf
  use iotk_str_interf
  use iotk_xtox_interf
  use iotk_unit_interf
  use iotk_misc_interf
  implicit none
  integer,                 intent(in)  :: unit
  character(iotk_attlenx), intent(in)  :: attr
  integer,                 intent(out) :: ierr
  character(iotk_vallenx) :: file_version,extensions
  logical :: binary,rbinary,check,found,stream
  integer :: pos1,pos2,attlen,itmp_major,itmp_minor
  ierr = 0
  call iotk_scan_attr(attr,"file_version",file_version,eos=.true.,ierr=ierr,found=found)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  if(found) then
    attlen = iotk_strlen(file_version)
    pos1   = iotk_strscan(file_version,".")
    if(pos1<=1 .or. pos1>=attlen) then
      $(ERROR ierr 'Problems reading file version' file_version attlen pos1)
      return
    end if
    pos2   = pos1 + verify(file_version(pos1+1:attlen),numbers)
    if(pos2==pos1+1) then
      $(ERROR ierr 'Problems reading file version' file_version attlen pos1 pos2)
      return
    end if
    if(pos2==pos1) pos2 = attlen+1
    call iotk_atoi(itmp_major,file_version(1:pos1-1),check)
    if(.not.check) then
      $(ERROR ierr 'Problems reading file version' file_version)
      return
    end if
    call iotk_atoi(itmp_minor,file_version(pos1+1:pos2-1),check)
    if(.not.check) then
      $(ERROR ierr 'Problems reading file version' file_version)
      return
    end if
    if(itmp_major > iotk_file_version_major .or. &
      (itmp_major==iotk_file_version_major .and. itmp_minor > iotk_file_version_minor) ) then
      $(ERROR ierr 'File version is newer than internal version' file_version internal_version=iotk_file_version)
      return
    end if
  end if
  call iotk_scan_attr(attr,"binary",rbinary,ierr=ierr,found=found)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  if(found) then
    call iotk_inquire(unit,binary,stream,ierr)
    if(ierr/=0) then
      $(ERROR ierr)
      return
    end if
    if(rbinary .neqv. binary) then
      $(ERROR ierr)
      return
    end if
  end if
  call iotk_scan_attr(attr,"extensions",extensions,ierr=ierr,found=found,eos=.true.)
  if(ierr/=0) then
    $(ERROR ierr)
    return
  end if
  if(found) then
    if(iotk_strlen(extensions) > 0) then
      $(ERROR ierr 'Extensions are not supported in this version' extensions)
      return
    end if
  end if
end subroutine iotk_check_iotk_attr_x

>PROCEDURE=iotk_index
function iotk_index_scal(index)
  use iotk_base
  use iotk_xtox_interf
  use iotk_misc_interf
  integer,           intent(in) :: index
  character(len=range(index)+3) :: iotk_index_scal
  iotk_index_scal="."//iotk_itoa(index)
end function iotk_index_scal
  
>PROCEDURE=iotk_index
function iotk_index_vec(index)
  use iotk_base
  use iotk_xtox_interf
  use iotk_misc_interf
  implicit none
  integer,                         intent(in) :: index(:)
  character(len=(range(index)+3)*size(index)) :: iotk_index_vec
  integer :: length,i
  length = 0
  iotk_index_vec = " "
  do i = 1,size(index)
    iotk_index_vec(length+1:length+1+(range(index)+3)) = "."//iotk_itoa(index(i))
    length = len_trim(iotk_index_vec)
  end do
end function iotk_index_vec


>PROCEDURE=iotk_tag_parse
subroutine iotk_tag_parse_x(tag,name,attr,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_misc_interf
  use iotk_str_interf
  implicit none
  character(iotk_taglenx), intent(in)  :: tag
  character(iotk_namlenx), intent(out) :: name
  character(iotk_attlenx), intent(out) :: attr
  integer,                 intent(out) :: ierr
  integer :: pos,lenatt,lentag
  ierr = 0
  lentag=iotk_strlen(tag)
  if(verify(tag(1:1),iotk_namcharfirst)/=0) then
    $(ERROR ierr 'Wrong syntax in tag')
    call iotk_error_write(ierr,"tag",tag(1:lentag))
    return
  end if
  pos = scan(tag(1:lentag)," ")
  if(pos==0) pos=lentag+1
  if(pos>len(name)+1) then
    $(ERROR ierr 'Tag name too long')
    return
   end if
  name = tag(1:pos-1)
  if(pos<=len(name)) name(pos:pos) = iotk_eos
  lenatt = len_trim(tag(pos:lentag))
  if(lenatt>iotk_attlenx) then
    $(ERROR ierr 'Attribute string too long')
    return
  end if
  if(lenatt>0) then
    attr(1:lenatt) = tag(pos:pos+lenatt-1)
    if(lenatt+1<=len(attr)) attr(lenatt+1:lenatt+1)=iotk_eos
  else
    attr(1:1)=iotk_eos
  end if
end subroutine iotk_tag_parse_x

>PROCEDURE=iotk_complete_filepath
function iotk_complete_filepath_x(newfile,oldfile)
  use iotk_base
  use iotk_misc_interf
  implicit none
  character(len=*), intent(in) :: newfile
  character(len=*), intent(in) :: oldfile
  character(len=len(newfile)+len(oldfile)) :: iotk_complete_filepath_x
  character(len=len(oldfile)) :: prefix
  integer :: pos
  if(newfile(1:1)=="/") then
    iotk_complete_filepath_x = newfile
  else
    pos = scan(oldfile,"/",back=.true.)
    prefix = " "
    if(pos>0) prefix = oldfile(1:pos)
    iotk_complete_filepath_x = trim(prefix)//trim(newfile)
  end if
end function iotk_complete_filepath_x

>PROCEDURE=iotk_check_name
function iotk_check_name_x(name)
  use iotk_base
  use iotk_misc_interf
  use iotk_str_interf
  implicit none
  character(len=*), intent(in) :: name
  logical                      :: iotk_check_name_x
! Checks a single name
  integer :: len_name
  iotk_check_name_x = .true.
  len_name = iotk_strlen_trim(name)
  if(len_name>iotk_namlenx) iotk_check_name_x = .false.
  if(verify(name(1:1),iotk_namcharfirst)/=0) iotk_check_name_x = .false.
  if(len_name>1) then
    if(verify(name(2:len_name),iotk_namchar)/=0) iotk_check_name_x = .false.
  end if
end function iotk_check_name_x

>PROCEDURE=iotk_delete_attr
subroutine iotk_delete_attr_x(attr,name,ierr)
  use iotk_base
  use iotk_str_interf
  use iotk_error_interf
  implicit none
  character(len=*), intent(inout) :: attr
  character(len=*), intent(in)    :: name
  integer,          intent(out)   :: ierr
  integer :: attlen,pos,equal,begin
  logical :: foundl
  character :: delim
  ierr = 0
  attlen=iotk_strlen(attr)
  foundl = .false.
  equal = 0
  begin = 0
  do
    pos = verify(attr(equal+1:attlen)," ")
    if(pos<=0) exit
    equal = equal + pos
    pos = scan(attr(equal+1:attlen),"=")
    if(pos<=0) then
      $(ERROR ierr '' attr equal)
      return
    end if
    if(trim(attr(equal:equal+pos-1))==trim(name)) foundl = .true.
    begin = equal
    equal = equal + pos
    pos   = verify(attr(equal+1:attlen)," ")
    if(pos<=0) then
      $(ERROR ierr)
      return
    end if
    equal = equal + pos
    delim = attr(equal:equal)
    if(delim/="'" .and. delim/='"') then
      $(ERROR ierr delim)
      return
    end if
    pos = scan(attr(equal+1:attlen),delim)
    if(pos<=0) then
      $(ERROR ierr)
      return
    end if
    equal = equal + pos
    if(foundl) exit
  end do
  if(foundl) then
    if(equal<attlen) then
      pos = verify(attr(equal+1:attlen)," ")
      if(pos==0) then
        equal=attlen
      else
        equal=equal+pos-1
      end if
    end if
    if(equal<attlen) then
      attr(begin:begin+attlen-equal) = attr(equal+1:attlen)//iotk_eos
    else
      attr(begin:begin)=iotk_eos
    end if
  end if
end subroutine iotk_delete_attr_x

subroutine iotk_readcmdline_x(args,nargs,eos,ierr)
  use iotk_base
  use iotk_error_interf
  use iotk_str_interf
  implicit none
  character(len=*),  intent(out) :: args(:)
  integer,           intent(out) :: nargs
  logical, optional, intent(in)  :: eos
  integer, optional, intent(out) :: ierr
  integer :: iarg,iostat,pos,ierrl
  logical :: leos

  ierrl = 0
  iarg = 0
  leos=.false.
  nargs = 0
  if(present(eos))leos=eos
  do
    iarg = iarg + 1
    if(iarg>size(args)) then
      $(ERROR ierrl)
      goto 1
    end if
    read(*,"(a)",iostat=iostat) args(iarg)
    if(iostat<0) exit
    if(iostat>0) then
      $(ERROR ierrl)
      goto 1
    end if
    pos = scan(args(iarg),"|",back=.true.)
    if(pos>0) then
      if(args(iarg)(pos:)=="|") then
        args(iarg)(pos:pos) = iotk_eos
      end if
    else
      pos = len_trim(args(iarg)) + 1
      if(pos<=len(args)) args(iarg)(pos:pos) = iotk_eos
    end if
    if(.not. leos) then
      pos = iotk_strlen(args(iarg))
      if(pos<len(args(iarg))) args(iarg)(pos+1:) = " "
    end if
  end do
  iarg = iarg -1
  nargs = iarg
1 continue
  if(present(ierr)) then
    ierr = ierrl
  else
    if(ierrl/=0) call iotk_error_handler(ierrl)
  end if
end subroutine iotk_readcmdline_x

subroutine iotk_init_static_vars_x()
  use iotk_base
  implicit none
  logical, save :: first=.true.
  if(first) then
    iotk_error_pool_used     = .false.
    iotk_error_pool_order    = 0
    iotk_unitmin             = iotk_unitmin_def
    iotk_unitmax             = iotk_unitmax_def
    iotk_error_unit          = iotk_error_unit_def
    iotk_output_unit         = iotk_output_unit_def
    iotk_getline_buffer      = iotk_getline_buffer_def
    iotk_linlen              = iotk_linlen_def
    iotk_indent              = iotk_indent_def
    iotk_maxindent           = iotk_maxindent_def
    iotk_error_warn_overflow = iotk_error_warn_overflow_def
  end if
  first=.false.
end subroutine iotk_init_static_vars_x



